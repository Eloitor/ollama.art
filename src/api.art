;===============================================
; Claude.ai.art
;
; Claude API wrapper
; for Arturo
;
; MIT License
; (c) 2024 Yanis Zafirópulos
;==========================================================
; @file: src/api.art
;==========================================================

;--------------------------
; Type Definitions 
;--------------------------

define :message [
    init: constructor [role :string content :string][
        this\role: role
        this\content: content
    ]
]

define :claudeClient [
    init: constructor [key :string][
        if null? key [ 
            throw.as: valueError "API key is required"
        ]
        if not? prefix? key "sk-" [
            throw.as: valueError "Invalid API key format - must start with 'sk-'"
        ]

        this\apiKey: key
        this\baseUrl: "https://api.anthropic.com"
        this\apiVersion: "2023-06-01"
        this\model: "claude-3-sonnet-20240229"
        this\maxTokens: 4096

        ; default parameters
        this\defaultTemp: 0.7
        this\defaultTopP: 1.0
    ]

    validateMessage: method [msg :message][
        if not? key? msg 'role [
            throw.as: valueError "Message must have a 'role' field"
        ]
        if not? key? msg 'content [
            throw.as: valueError "Message must have a 'content' field"
        ]
        if not? in? msg\role ["user" "assistant"] [
            throw.as: valueError "Message role must be 'user' or 'assistant'"
        ]
    ]

    chat: method [messages :block][
        ;; description: « Send a chat request to Claude's API
        ;; options: [
        ;;     model: :string « specify Claude model to use (default: claude-3-sonnet)
        ;;     system: :string « specify system message
        ;;     temperature: :floating « set temperature (0.0-1.0)
        ;;     maxTokens: :integer « set max tokens for response
        ;;     topP: :floating « set top_p value (0.0-1.0)
        ;;     topK: :integer « set top_k value
        ;; ]
        ;; returns: :dictionary
        ;; example: {
        ;;     claude: to :claudeClient ["sk-..."]
        ;;     result: claude\chat [
        ;;         to :message ["user" "What is 2+2?"]
        ;;     ]
        ;;     print result\content
        ;; }

        ; Validate messages
        loop messages this\validateMessage

        ; Get parameters from attributes or defaults
        model: (attr 'model) ?? this\model
        system: attr 'system
        temperature: (attr 'temperature) ?? this\defaultTemp
        maxTokens: (attr 'maxTokens) ?? this\maxTokens
        topP: (attr 'topP) ?? this\defaultTopP
        topK: attr 'topK

        ; Build request body
        body: #[
            model: model
            messages: messages
            max_tokens: maxTokens
            temperature: temperature
            top_p: topP
        ]

        if not? null? system [
            set body 'system system
        ]

        if not? null? topK [
            set body 'top_k topK
        ]

        ; Make API request
        result: request.post 
            .json
            .headers: #[
                "x-api-key": this\apiKey
                "anthropic-version": this\apiVersion
                "content-type": "application/json"
            ]
            this\baseUrl ++ "/v1/messages"
            body

        if result\status <> 200 [
            throw.as: runtimeError join @[
                "API request failed with status" 
                result\status ":" 
                read.json result\body
            ]
        ]

        return read.json result\body
    ]

    setModel: method [model :string][
        ;; description: « Set default model
        this\model: model
    ]

    setMaxTokens: method [tokens :integer][
        ;; description: « Set default max tokens
        this\maxTokens: tokens
    ]

    setTemperature: method [temp :floating][
        ;; description: « Set default temperature
        this\defaultTemp: temp
    ]

    setTopP: method [topP :floating][
        ;; description: « Set default top_p
        this\defaultTopP: topP
    ]
]

;--------------------------
; Helper Functions
;--------------------------

createMessage: function [role :string content :string][
    ;; description: « Create a new message object
    ;; returns: :message
    to :message [role content]
]